mod INV {
  pr(NSLPK)
  vars A B A2 B2 A3 B3 A4 : Prin 
  var SK : PriKey
  vars D D1 D2 : Data
  vars DL1 DL2 DL3 : DataL
  vars S : Sys
  vars N N2 N3 : Nonce

-- checking the protocol execution
  op check : Sys Prin Prin Nonce Nonce -> Bool
  eq check(S,A,B,N,N2) = 
    (msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
     msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
     msg3(A,A,B, aenc(pubK(B), N2)) \in nw(S)) .

  op keySe : Sys Prin Prin Prin Nonce Nonce Data -> Bool
  eq keySe(S,B2,A,B,N,N2,D) = 
  (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
    msg2(B2,B,A, D) \in nw(S) and 
    adec(priK(A), D) = N || N2 || B and
    msg3(A,A,B, aenc(pubK(B), N2)) \in nw(S))
   implies not(N \in knl(S)) .

  op keySe2 : Sys Prin Prin Prin Nonce Nonce Data Data -> Bool
  eq keySe2(S,A2,A,B,N,N2,D,D2) = 
  (not(A = intru or B = intru) and
    msg1(A2,A,B, D) \in nw(S) and 
    adec(priK(B), D) = A || N and 
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    msg3(A2,A,B, D2) \in nw(S) and 
    adec(priK(B), D2) = N2)
   implies not(N2 \in knl(S)) .

  op inv0 : Sys Prin Prin Nonce Nonce -> Bool
  eq inv0(S,A,B,N,N2) = 
  (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S))
   implies not(N2 \in knl(S)) .

-- 2+3+4+8
  op inv1 : Sys Prin Prin Nonce -> Bool
  eq inv1(S,A,B,N) = 
   (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S))
   implies not(N \in knl(S)) .

-- 5
  op inv2 : Sys Prin Prin Prin Nonce -> Bool
  eq inv2(S,A2,A,B,N) = 
    msg1(A2,A,B, aenc(pubK(B), A || N)) \in nw(S)
   implies N \in unonce(S) .

-- 5
  op inv3 : Sys Nonce -> Bool
  eq inv3(S,N) = N \in knl(S)
   implies N \in unonce(S) .

-- 2+5+7+lm1+10+12
  op inv4 : Sys Prin Prin Nonce DataL -> Bool
  eq inv4(S,A,B,N,DL1) = 
   (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
    aenc(pubK(intru), DL1) \in knl(S) and
    N \in DL1)
   implies N \in knl(S) .

-- 2+3+6+7+lm1
  op inv5 : Sys Prin Nonce DataL -> Bool
  eq inv5(S,A,N,DL1) = 
   (aenc(pubK(A), DL1) \in knl(S) and
    N \in DL1)
   implies N \in unonce(S) .

-- 5
  op inv6 : Sys Prin Prin Prin Nonce Nonce -> Bool
  eq inv6(S,A2,A,B,N,N2) = 
    msg2(A2,A,B, aenc(pubK(B), N || N2 || A)) \in nw(S)
   implies (N \in unonce(S) and N2 \in unonce(S)) .

-- 7+lm1
  op inv7 : Sys Prin Prin DataL DataL -> Bool
  eq inv7(S,A,B,DL1,DL2) = 
   (aenc(pubK(A), DL1) \in knl(S) and
    aenc(pubK(B), DL2) \in DL1)
   implies DL1 \in knl(S) .

-- 9
  op inv8 : Sys Prin -> Bool
  eq inv8(S,A) = priK(A) \in knl(S)
    implies (A = intru) .

-- 1+7
  op inv9 : Sys Prin Prin DataL -> Bool
  eq inv9(S,A,B,DL1) = 
   (aenc(pubK(A), DL1) \in knl(S) and
    priK(B) \in DL1)
   implies priK(B) \in knl(S) .

-- 2+11
  op inv10 : Sys Prin Prin Prin Prin Nonce -> Bool
  eq inv10(S,A,B,A2,B2,N) = 
    not(not(A = intru or B = intru) and
        msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
        msg1(A2,intru,B2, aenc(pubK(B2), intru || N)) \in nw(S)) .

-- 1+2+5+7+lm1
  op inv11 : Sys Prin Prin Prin Nonce -> Bool
  eq inv11(S,A,B,B2,N) = 
    not(not(A = intru or B = intru) and
        msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
        aenc(pubK(B2), intru || N) \in knl(S)) .

-- 2+6+13
  op inv12 : Sys Prin Prin Prin Prin Nonce Nonce -> Bool
  eq inv12(S,A,B,A2,B2,N,N2) = 
    not(not(A = intru or B = intru) and
        msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
        msg2(A2,intru,B2, aenc(pubK(B2), N2 || N || intru)) \in nw(S)) .

-- 1+2+5+7+lm1
  op inv13 : Sys Prin Prin Prin Nonce Nonce -> Bool
  eq inv13(S,A,B,B2,N,N2) = 
    not(not(A = intru or B = intru) and
        msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
        aenc(pubK(B2), N2 || N || intru) \in knl(S)) .

-- 5+6+7+15+16+lm1
  op inv14 : Sys Prin Prin Nonce Nonce DataL -> Bool
  eq inv14(S,A,B,N,N2,DL1) = 
   (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
    aenc(pubK(intru), DL1) \in knl(S) and
    N2 \in DL1)
   implies N2 \in knl(S) .

-- 2+6+17
  op inv15 : Sys Prin Prin Prin Prin Nonce Nonce -> Bool
  eq inv15(S,A,B,A2,B2,N,N2) = 
  not(not(A = intru or B = intru) and
      msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
      msg1(A2,intru,B2, aenc(pubK(B2), intru || N2)) \in nw(S)) .

-- 6+18
  op inv16 : Sys Prin Prin Prin Prin Nonce Nonce Nonce -> Bool
  eq inv16(S,A,B,A2,B2,N,N2,N3) = 
  not(not(A = intru or B = intru) and
      msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
      msg2(A2,intru,B2, aenc(pubK(B2), N3 || N2 || intru)) \in nw(S)) .

-- 0+5+6+7+lm1
  op inv17 : Sys Prin Prin Prin Nonce Nonce -> Bool
  eq inv17(S,A,B,B2,N,N2) = 
  not(not(A = intru or B = intru) and
      msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
      aenc(pubK(B2), intru || N2) \in knl(S)) .

-- 0+5+6+7+lm1
  op inv18 : Sys Prin Prin Prin Nonce Nonce Nonce -> Bool
  eq inv18(S,A,B,B2,N,N2,N3) = 
  not(not(A = intru or B = intru) and
      msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
      aenc(pubK(B2), N3 || N2 || intru) \in knl(S)) .

-- 6+19
  op auth : Sys Prin Prin Prin Nonce Nonce Data -> Bool
  eq auth(S,B2,A,B,N,N2,D) = 
  (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
    msg2(B2,B,A, D) \in nw(S) and 
    adec(priK(A), D) = N || N2 || B)
   implies msg2(B,B,A, D) \in nw(S) .

-- 20+23
  op auth2 : Sys Prin Prin Prin Nonce Nonce Data -> Bool
  eq auth2(S,A2,A,B,N,N2,D2) = 
  (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    msg3(A2,A,B, D2) \in nw(S) and 
    adec(priK(B), D2) = N2)
   implies msg3(A,A,B, D2) \in nw(S) .

-- no induction
-- auth2 + 24
  op auth3 : Sys Prin Prin Prin Nonce Nonce Data -> Bool
  eq auth3(S,A2,A,B,N,N2,D2) = 
  (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    msg3(A2,A,B, D2) \in nw(S) and 
    adec(priK(B), D2) = N2)
   implies msg1(A,A,B, D2) \in nw(S) .

-- 1+5+7+lm1
  op inv19 : Sys Prin Prin Nonce Nonce -> Bool
  eq inv19(S,A,B,N,N2) = 
  (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
    aenc(pubK(A), N || N2 || B) \in knl(S))
   implies msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) .

-- 0+5+7+21+lm1
  op inv20 : Sys Prin Prin Nonce Nonce -> Bool
  eq inv20(S,A,B,N,N2) = 
  (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    aenc(pubK(B), N2) \in knl(S))
   implies msg3(A,A,B, aenc(pubK(B), N2)) \in nw(S) .

-- 6+22
  op inv21 : Sys Prin Prin Prin Prin Nonce Nonce Nonce -> Bool
  eq inv21(S,A,B,A2,B2,N,N2,N3) = 
   (msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
    msg2(B2,B,A2, aenc(pubK(A2), N3 || N2 || B)) \in nw(S) and
    not(B = intru or A = intru))
   implies (A = A2) .

-- 0+5+6+7+lm1
  op inv22 : Sys Prin Prin Prin Nonce Nonce Nonce -> Bool
  eq inv22(S,A,B,A2,N,N2,N3) = 
   (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
    aenc(pubK(A2), N3 || N2 || B) \in knl(S))
   implies (A = A2) .

-- 5+6
  op inv23 : Sys Prin Prin Prin Nonce -> Bool
  eq inv23(S,A2,A,B,N2) = 
    msg3(A2,A,B, aenc(pubK(B), N2)) \in nw(S)
   implies N2 \in unonce(S) .

-- 23+25
  op inv24 : Sys Prin Prin Nonce Nonce Data -> Bool
  eq inv24(S,A,B,N,N2,D2) = 
  (not(A = intru or B = intru) and
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    msg3(A,A,B, D2) \in nw(S) and 
    adec(priK(B), D2) = N2)
   implies msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) .

-- 6+26
  op inv25 : Sys Prin Prin Prin Nonce Nonce Nonce -> Bool
  eq inv25(S,A,B,B2,N,N2,N3) = 
   (msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
    msg2(B2,B,A, aenc(pubK(A), N3 || N2 || B)) \in nw(S) and
    not(B = intru or A = intru))
   implies (N = N3) .

-- 0+5+6+7+lm1
  op inv26 : Sys Prin Prin Nonce Nonce Nonce -> Bool
  eq inv26(S,A,B,N,N2,N3) = 
   (msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and
    aenc(pubK(A), N3 || N2 || B) \in knl(S) and
    not(B = intru or A = intru))
   implies (N = N3) .

-- others
  op lm1 : Data DataL DataL -> Bool
  eq lm1(D1,DL1,DL2) = (D1 \in DL1 and DL1 \in DL2)
    implies D1 \in DL2 .

  op keySe3 : Sys Prin Prin Prin Nonce Nonce Data -> Bool
  eq keySe3(S,B2,A,B,N,N2,D) = 
  (not(A = intru or B = intru) and
    msg1(A,A,B, aenc(pubK(B), A || N)) \in nw(S) and
    msg2(B2,B,A, D) \in nw(S) and 
    adec(priK(A), D) = N || N2 || B and
    msg3(A,A,B, aenc(pubK(B), N2)) \in nw(S))
   implies not(N2 \in knl(S)) .

  op keySe4 : Sys Prin Prin Prin Nonce Nonce Data Data -> Bool
  eq keySe4(S,A2,A,B,N,N2,D,D2) = 
  (not(A = intru or B = intru) and
    msg1(A2,A,B, D) \in nw(S) and 
    adec(priK(B), D) = A || N and 
    msg2(B,B,A, aenc(pubK(A), N || N2 || B)) \in nw(S) and 
    msg3(A2,A,B, D2) \in nw(S) and 
    adec(priK(B), D2) = N2)
   implies not(N \in knl(S)) .
}